import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.7.20'
}
apply from: 'gradle/properties.gradle'

group = info.group
version = "${info.version}-${version_spigot}"

sourceCompatibility = 18
targetCompatibility = 18

test {
    useJUnitPlatform()
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly deps.spigot
    compileOnly deps.log4j
    compileOnly deps.snakeyaml
    implementation deps.kotlin
    implementation deps.koin.core
    implementation 'org.xerial:sqlite-jdbc:3.40.0.0'
    implementation "org.jetbrains.exposed:exposed-core:$version_exposed"
    implementation "org.jetbrains.exposed:exposed-dao:$version_exposed"
    implementation "org.jetbrains.exposed:exposed-jdbc:$version_exposed"

    testImplementation deps.spigot
    testImplementation 'org.jetbrains.kotlin:kotlin-reflect'
    testImplementation "org.junit.jupiter:junit-jupiter:5.9.1"
    testImplementation 'org.mockito:mockito-core:4.10.0'
    testImplementation 'org.mockito:mockito-inline:4.10.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.1.0'
    testImplementation deps.koin.test
    testImplementation deps.koin.test_junit5
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [
                'version': version.toString(),
                'api-version': version_spigot_api.toString(),
        ]
    }
}

jar {
    exclude "META-INF/versions/9/module-info.class"
    from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
}

task copyJarToServer(type: Copy) {
    from jar
    into "server/plugins"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

assemble.dependsOn copyJarToServer